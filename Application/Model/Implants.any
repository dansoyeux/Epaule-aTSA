/* -------- IMPLANT LOADING --------
Position & Name for implants: 
Call to custom file based on template "InputPosition_ImplantName.any"
----------------------------- */


//Files with the original humeral implant position
#path INPUTPOSFILE "../Input/Implants/ImplantPositions/InputImplantJoint_" + INPUTPOS + ".any"
#include "<INPUTPOSFILE>" 

/* -------- GLENOID IMPLANT-----------------------------------------------------*/
Main.HumanModel.BodyModel.Right.ShoulderArm.Seg.Scapula = {
  
  AnyFolder &Param = Main.InputVariables.ImplantParameters;
  
  AnyRefNode GlenImplantPos = {
    sRel = .Param.GlenPosition.Position*.Mirror;
    ARel = .Param.GlenPosition.Rotation*.Mirror;
    
    AnyDrawRefFrame drw = {
      Visible= Off;
      ScaleXYZ={1,1,1}/12;
      RGB = {1,0,0};
    }; 
    
    
    AnySurfSTL GleneSTL = {
      FileName = "../Input/Implants/ImplantModels/" + ..Param.ImplantNames.GlenoidName + ".stl";
      ScaleXYZ = {1,1,1}*0.001;
      AnyDrawSurf drw = {
        FileName = .FileName;
        RGB = {1,0.2,0}; // red
        // RGB = {221, 48, 131} / 255; // magenta
        // RGB = {0, 1, 0}; // green
        // RGB = {0, 0.2, 1}; // blue
        // RGB = {16, 132, 244} / 255; // light blue
        
        ScaleXYZ = {1,1,1}*0.001;
      };
    };// GleneSTL      
    
  }; // GlenImplantPos
  
  
  gh = {
    
    AnyDrawRefFrame drwref = {
      Visible = Off;
      RGB = {0.5,0.6,0.3};
      ScaleXYZ = {1,1,1}/15;
    };
  };
  
  
  
  //ghProth is the center of a sphere fitted to glenoid implant surface (see script file)
  AnyRefNode ghProth = {
    sRel = .Param.GlenPosition.Center_Absolute;    
    ARel = .Param.GlenPosition.Rotation;
    AnyDrawRefFrame drwref = {
      Visible = Off;
      RGB = {1,0,0}; // red
      //      RGB = {0,0,1}; // blue
      ScaleXYZ = {1,1,1}/15;
    }; 
    
  };//ghProth
  
  // ghProth oriented like aligned with the ISB scapula coordinate system
  AnyRefNode ghProth_ISB = {
    sRel = .Param.GlenPosition.Center_Absolute;    
    ARel={{ 0, 0, 1}, { 0, 1,0}, {-1, 0,0}};
    AnyDrawRefFrame drwref = {
      Visible = Off;
      RGB = {1,0,0}; // red
      ScaleXYZ = {1,1,1}/15;
    }; 
    
  };
  
};//Scapula


/* -------- HUMERAL IMPLANT--------------------------------------------------------------*/
//Enlever cet offset en appliquant cet offset aux coordonnes de base de l'implant humeral
Main.HumanModel.BodyModel.Right.ShoulderArm.Seg.Humerus = {
  
  //Displays Humerus.gh node
  gh = {
    AnyDrawRefFrame drw = {
      Visible = Off;
      RGB = {0,1,0};
      ScaleXYZ = {1,1,1}/15;
      
    };
    
  }; //gh humerus
  
  
  AnyFolder &Param = Main.InputVariables.ImplantParameters;
  
  AnyRefNode HumImplantPos = {
    sRel = .Param.HumPosition.Position;
    ARel = .Param.HumPosition.Rotation;
    
    AnyDrawRefFrame drw = {
      Visible = Off;
      RGB={0,0,1};        
      ScaleXYZ={1,1,1}*0.06;
      
    };  
    AnySurfSTL ProtHumSTL = {
      FileName = "../Input/Implants/ImplantModels/" + ..Param.ImplantNames.HumerusName + ".stl";
      ScaleXYZ = {1,1,1}*0.001;
      
      AnyDrawSurf drw= {
        FileName = .FileName;
        RGB = {0.2,0.4,0.5};
        ScaleXYZ = {1,1,1}*0.001;
        Opacity = 0.7;
      };
      
    }; // ProtHumSTL    
    
    AnyRefNode ghProth = {
      sRel=Main.InputVariables.ImplantParameters.HumPosition.Center_Local;
      
      AnyDrawRefFrame drw = {
        Visible = Off;
        RGB={0,0,1};        
        ScaleXYZ={1,1,1}*0.06;
        
      };  
    }; //ghProth
    
  }; // HumImplantPos 
  
};//Humerus









